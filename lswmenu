#!/usr/bin/env perl
# See LICENSE file for copyright and license details.
use strict;
use warnings;
#use diagnostics;
use IPC::Open2;

my $VERBOSE = 0;
my $WINJMP = 'wmctrl';
my %WNDs = ();

sub lswin {
	my @args = @_;

	print "Open pipe into 'lsw @args'\n" if $VERBOSE;

	open(my $lsw_fh, '-|', "lsw @args")
		or die "$0: couldn't open a pipe into lsw: $!\n";

	while (my $line = <$lsw_fh>) {
		chomp $line;

		my ($wid, $text) = ($1, $2)
			if $line =~ /^(0x[0-9a-h]{7}) (.*)$/;

		print "Window $wid: $text\n" if $VERBOSE;

		$WNDs{ $wid } = length($text) ? $text : "(null)";
	}

	close $lsw_fh;

	return keys %WNDs;
}

sub dmenu_sel_win {
	my @args = @_;
	my $selwin;

	print "Open pipe into 'dmenu @args'\n" if $VERBOSE;

	open2(my $chld_out, my $chld_in, 'dmenu', @args)
		or die "$0: open2() failed: $!\n";

	print $chld_in "$WNDs{ $_ }\n" for sort keys %WNDs;
	close $chld_in;

	while (<$chld_out>) {
		chomp;
		$selwin .= $_;
	}
	close $chld_out;

	print "Selected '$selwin'.\n" if $VERBOSE;

	return $selwin;
}

sub goto_sel_win {
	my $wid = shift;

	if ($WINJMP eq 'xdotool') {
		print "Execute 'xdotool windowactivate $wid'\n" if $VERBOSE;
		system("xdotool windowactivate '$wid'");
	} else {
		print "Execute 'wmctrl -i -a $wid'\n" if $VERBOSE;
		system("wmctrl -i -a '$wid'");
	}
}

sub main {
	my @lswargs;
	my @dmenuargs;

	for (@ARGV) {
		if    (/\bverbose\b/)         { $VERBOSE++          }
		elsif (/\bxdotool\b/)         { $WINJMP = 'xdotool' }
		elsif (/\b(0x[0-9a-h]{7})\b/) { push @lswargs,   $_ }
		else                          { push @dmenuargs, $_ }
	}

	print <<EOF if $VERBOSE > 1;
--------------------------------
Verbosity level:  $VERBOSE
Window jumper:    $WINJMP
lsw(1) optargs:   @lswargs
dmenu(1) optargs: @dmenuargs
--------------------------------
EOF

	lswin(@lswargs)
		or die "$0: no windows available\n";

	my $sel = dmenu_sel_win(@dmenuargs)
		or die "$0: cannot select window\n";

	for my $wid (keys %WNDs) {
		if ($WNDs{ $wid } eq $sel) {
			goto_sel_win($wid) and last;
		}
	}
}

main() if not caller;
1;
